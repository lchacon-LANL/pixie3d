c-----------------------------------------------------------------------
c     file debug2.f.
c     prints large us.
c-----------------------------------------------------------------------
c-----------------------------------------------------------------------
c     code organization.
c-----------------------------------------------------------------------
c     0. debug_mod.
c     1. debug1.
c     2. debug2.
c     3. debug3.
c-----------------------------------------------------------------------
c     subprogram 0. debug_mod.
c     module declarations.
c-----------------------------------------------------------------------
      MODULE debug_mod
      USE local_mod
      IMPLICIT NONE

      CONTAINS
c-----------------------------------------------------------------------
c     subprogram 1. debug1.
c     prints 1d arrays.
c-----------------------------------------------------------------------
c-----------------------------------------------------------------------
c     declarations.
c-----------------------------------------------------------------------
      SUBROUTINE debug1(u,name,unit)

      REAL(r8), DIMENSION(:,:), INTENT(IN) :: u
      CHARACTER(*) :: name
      INTEGER :: unit

      CHARACTER(80) :: format1,format2
      INTEGER :: nqty,nx,iqty,i
c-----------------------------------------------------------------------
c     format statements.
c-----------------------------------------------------------------------
 10   FORMAT('(/3x,"i",',i1,'(6x,i1,4x)/)')
 20   FORMAT('(i4,1p,',i1,'e11.3)')
c-----------------------------------------------------------------------
c     define sizes.
c-----------------------------------------------------------------------
      nqty=SIZE(u,1)
      nx=SIZE(u,2)
c-----------------------------------------------------------------------
c     create formats.
c-----------------------------------------------------------------------
      WRITE(format1,10)nqty
      WRITE(format2,20)nqty
c-----------------------------------------------------------------------
c     write array.
c-----------------------------------------------------------------------
      WRITE(unit,'(a)')TRIM(name)//":"
      WRITE(unit,format1)(iqty,iqty=1,nqty)
      DO i=1,nx
         WRITE(unit,format2)i,u(:,i)
      ENDDO
      WRITE(unit,format1)(iqty,iqty=1,nqty)
c-----------------------------------------------------------------------
c     terminate.
c-----------------------------------------------------------------------
      RETURN
      END SUBROUTINE debug1
c-----------------------------------------------------------------------
c     subprogram 2. debug2.
c     prints 2d arrays.
c-----------------------------------------------------------------------
c-----------------------------------------------------------------------
c     declarations.
c-----------------------------------------------------------------------
      SUBROUTINE debug2(u,name,unit)

      REAL(r8), DIMENSION(:,:,:), INTENT(IN) :: u
      CHARACTER(*) :: name
      INTEGER :: unit

      CHARACTER(80) :: format1,format2
      INTEGER :: nqty,nx,ny,iqty,ix,iy
c-----------------------------------------------------------------------
c     format statements.
c-----------------------------------------------------------------------
 10   FORMAT('(/2x,"ix",2x,"iy",',i1,'(6x,i1,4x)/)')
 20   FORMAT('(2i4,1p,',i1,'e11.3)')
c-----------------------------------------------------------------------
c     define sizes.
c-----------------------------------------------------------------------
      nqty=SIZE(u,1)
      nx=SIZE(u,2)
      ny=SIZE(u,3)
c-----------------------------------------------------------------------
c     create formats.
c-----------------------------------------------------------------------
      WRITE(format1,10)nqty
      WRITE(format2,20)nqty
c-----------------------------------------------------------------------
c     write array.
c-----------------------------------------------------------------------
      WRITE(unit,'(a)')TRIM(name)//":"
      WRITE(unit,format1)(iqty,iqty=1,nqty)
      DO iy=1,ny
         DO ix=1,nx
            WRITE(unit,format2)ix,iy,u(:,ix,iy)
         ENDDO
         WRITE(unit,format1)(iqty,iqty=1,nqty)
      ENDDO
c-----------------------------------------------------------------------
c     terminate.
c-----------------------------------------------------------------------
      RETURN
      END SUBROUTINE debug2
c-----------------------------------------------------------------------
c     subprogram 3. debug3.
c     prints 2d arrays.
c-----------------------------------------------------------------------
c-----------------------------------------------------------------------
c     declarations.
c-----------------------------------------------------------------------
      SUBROUTINE debug3(u,name,unit)

      REAL(r8), DIMENSION(:,:,:,:), INTENT(IN) :: u
      CHARACTER(*) :: name
      INTEGER :: unit

      CHARACTER(80) :: format1,format2
      INTEGER :: nqty1,nqty2,nx,ny,iqty,jqty,ix,iy
c-----------------------------------------------------------------------
c     format statements.
c-----------------------------------------------------------------------
 10   FORMAT('(/2x,"ix",2x,"iy",',i1,'(5x,i1,",",i1,3x)/)')
 20   FORMAT('(2i4,1p,',i1,'e11.3)')
c-----------------------------------------------------------------------
c     define sizes.
c-----------------------------------------------------------------------
      nqty1=SIZE(u,1)
      nqty2=SIZE(u,2)
      nx=SIZE(u,3)
      ny=SIZE(u,4)
c-----------------------------------------------------------------------
c     create formats.
c-----------------------------------------------------------------------
      WRITE(format1,10)nqty1*nqty2
      WRITE(format2,20)nqty1*nqty2
c-----------------------------------------------------------------------
c     write array.
c-----------------------------------------------------------------------
      WRITE(unit,'(a)')TRIM(name)//":"
      WRITE(unit,format1)((iqty,jqty,iqty=1,nqty1),jqty=1,nqty2)
      DO iy=1,ny
         DO ix=1,nx
            WRITE(unit,format2)ix,iy,u(:,:,ix,iy)
         ENDDO
         WRITE(unit,format1)((iqty,jqty,iqty=1,nqty1),jqty=1,nqty2)
      ENDDO
c-----------------------------------------------------------------------
c     terminate.
c-----------------------------------------------------------------------
      RETURN
      END SUBROUTINE debug3
      END MODULE debug_mod
